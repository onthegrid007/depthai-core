# Add CMake testing infrastructure
include(CTest)
enable_testing()

# Add catch2 for writing tests
hunter_add_package(Catch2)
find_package(Catch2 CONFIG REQUIRED)

# Create environments for usb & poe labels
set(test_usb_env
    # Misc
    "UBSAN_OPTIONS=halt_on_error=1"
    # DepthAI
    "DEPTHAI_PROTOCOL=usb"
)
set(test_poe_env
    # Misc
    "UBSAN_OPTIONS=halt_on_error=1"
    # DepthAI
    "DEPTHAI_PROTOCOL=tcpshd;DEPTHAI_SEARCH_TIMEOUT=15000"
    )

include(CMakeParseArguments)

# Create a new onhost test
function(dai_add_onhost_test test_name test_src)

    # parse arguments
    cmake_parse_arguments(DAT "CONFORMING" "CXX_STANDARD" "" ${ARGN})

    # Create test executable
    add_executable(${test_name} ${test_src})
    add_default_flags(${test_name} LEAN)

    # Add to clangformat target
    if(COMMAND target_clangformat_setup)
        target_clangformat_setup(${test_name} "")
    endif()

    if(NOT DEPTHAI_MERGED_TARGET)
        target_link_libraries(${test_name} PRIVATE depthai::opencv ${OpenCV_LIBS} Catch2::Catch2WithMain Threads::Threads)
    else()
        target_link_libraries(${test_name} PRIVATE depthai::core ${OpenCV_LIBS} Catch2::Catch2WithMain Threads::Threads)
    endif()

    # Add sanitizers for tests as well
    if(COMMAND add_sanitizers)
        add_sanitizers(${test_name})
    endif()

    add_test(${test_name} ${test_name})
    set_tests_properties(${test_name} PROPERTIES LABELS "onhost")
endfunction()

# Create two tests for USB and PoE devices
function(dai_add_ondevice_test test_name test_src)

    # parse arguments
    cmake_parse_arguments(DAT "CONFORMING" "CXX_STANDARD" "" ${ARGN})

    # Create test executable
    add_executable(${test_name} ${test_src})
    add_default_flags(${test_name} LEAN)
    target_compile_options(${test_name} PRIVATE "${CMAKE_CXX${DAT_CXX_STANDARD}_STANDARD_COMPILE_OPTION}")

    # Add to clangformat target
    if(COMMAND target_clangformat_setup)
        target_clangformat_setup(${test_name} "")
    endif()

    # Link to core and Catch2 testing framework
    if(NOT DEPTHAI_MERGED_TARGET)
        target_link_libraries(${test_name} PRIVATE depthai::opencv ${OpenCV_LIBS} Catch2::Catch2WithMain Threads::Threads)
    else()
        target_link_libraries(${test_name} PRIVATE depthai::core ${OpenCV_LIBS} Catch2::Catch2WithMain Threads::Threads)
    endif()

    # Add sanitizers for tests as well
    if(COMMAND add_sanitizers)
        add_sanitizers(${test_name})
    endif()

    # Add USB test variant
    add_test(${test_name}_usb ${test_name})
    set_tests_properties(${test_name}_usb PROPERTIES ENVIRONMENT "${test_usb_env}" LABELS "ondevice_usb")

    # Add PoE test variant
    add_test(${test_name}_poe ${test_name})
    set_tests_properties(${test_name}_poe PROPERTIES ENVIRONMENT "${test_poe_env}" LABELS "ondevice_poe")

    # Add conforming variants
    if(MSVC AND DAT_CONFORMING) 
        # Create test executable with MSVC conforming preprocessor
        add_executable(${test_name}_conforming_usb ${test_src})
        add_default_flags(${test_name}_conforming_usb LEAN)
        add_executable(${test_name}_conforming_poe ${test_src})
        add_default_flags(${test_name}_conforming_poe LEAN)

        # Link to core and Catch2 testing framework
        if(NOT DEPTHAI_MERGED_TARGET)
            target_link_libraries(${test_name}_conforming_usb PRIVATE depthai::opencv ${OpenCV_LIBS} Catch2::Catch2WithMain Threads::Threads)
            target_link_libraries(${test_name}_conforming_poe PRIVATE depthai::opencv ${OpenCV_LIBS} Catch2::Catch2WithMain Threads::Threads)
        else()
            target_link_libraries(${test_name}_conforming_usb PRIVATE depthai::core ${OpenCV_LIBS} Catch2::Catch2WithMain Threads::Threads)
            target_link_libraries(${test_name}_conforming_poe PRIVATE depthai::core ${OpenCV_LIBS} Catch2::Catch2WithMain Threads::Threads)
        endif()

        # Set compiler c++ standard
        target_compile_options(${test_name}_conforming_usb PRIVATE "${CMAKE_CXX${DAT_CXX_STANDARD}_STANDARD_COMPILE_OPTION}")
        target_compile_options(${test_name}_conforming_poe PRIVATE "${CMAKE_CXX${DAT_CXX_STANDARD}_STANDARD_COMPILE_OPTION}")

        if(MSVC_VERSION LESS 1925)
            target_compile_options(${test_name}_conforming_usb PRIVATE "/experimental:preprocessor")
            target_compile_options(${test_name}_conforming_poe PRIVATE "/experimental:preprocessor")
        else()
            target_compile_options(${test_name}_conforming_usb PRIVATE "/Zc:preprocessor")
            target_compile_options(${test_name}_conforming_poe PRIVATE "/Zc:preprocessor")
        endif()

        # Add to list of tests
        add_test(${test_name}_conforming_usb ${test_name}_conforming_usb)
        add_test(${test_name}_conforming_poe ${test_name}_conforming_poe)

        # Add ubsan halt on error
        set_tests_properties(${test_name}_conforming_usb PROPERTIES ENVIRONMENT "${test_usb_env}" LABELS "${test_type}_usb")
        set_tests_properties(${test_name}_conforming_poe PROPERTIES ENVIRONMENT "${test_poe_env}" LABELS "${test_type}_poe")
    endif()

endfunction()

# Function for adding new tests
function(dai_add_test test_name test_src test_type)

    # Check test type
    if((NOT "${test_type}" STREQUAL "ondevice") AND (NOT "${test_type}" STREQUAL "onhost"))
        message(FATAL_ERROR "Invalid test type: ${test_type}. Must be either 'ondevice' or 'onhost'")
    endif()

    # parse arguments
    cmake_parse_arguments(DAT "CONFORMING" "CXX_STANDARD" "" ${ARGN})

    # check compiler for C++ standard
    if(NOT DAT_CXX_STANDARD)
        set(DAT_CXX_STANDARD 17)
    endif()
    if(NOT COMPILER_SUPPORTS_CXX${DAT_CXX_STANDARD})
        message(STATUS "Skipping test ${test_name} with c++${DAT_CXX_STANDARD}; no compiler support")
        return()
    endif()

    if(("${test_type}" STREQUAL "onhost"))
        dai_add_onhost_test(${test_name} ${test_src} ${ARGN})
    endif()

    if(("${test_type}" STREQUAL "ondevice"))
        dai_add_ondevice_test(${test_name} ${test_src} ${ARGN})
    endif()

    # Copy over required DLLs (Windows)
    if(WIN32)
        # Copy dlls to target directory - Windows only
        # TARGET_RUNTIME_DLLS generator expression available since CMake 3.21
        if(CMAKE_VERSION VERSION_LESS "3.21")
            file(GLOB depthai_dll_libraries "${HUNTER_INSTALL_PREFIX}/bin/*.dll")
        else()
            set(depthai_dll_libraries "$<TARGET_RUNTIME_DLLS:${test_name}>")
        endif()
        add_custom_command(TARGET ${test_name} POST_BUILD COMMAND
            "$<$<BOOL:${depthai_dll_libraries}>:${CMAKE_COMMAND};-E;copy_if_different;${depthai_dll_libraries};$<TARGET_FILE_DIR:${test_name}>>"
            COMMAND_EXPAND_LISTS
            VERBATIM
        )
    endif()
endfunction()

# Function for adding compile definitions to tests
function(dai_test_compile_definitions)
    if(TARGET ${ARGV0})
        target_compile_definitions(${ARGV})
        if(TARGET ${ARGV0}_conforming)
            set(ARGV0 ${ARGV0}_conforming)
            list(REMOVE_AT ARGV 0)
            list(INSERT ARGV 0 ${ARGV0})
            target_compile_definitions(${ARGV})
        endif()
    endif()
endfunction()

# Mobilenet network
hunter_private_data(
    URL "https://artifacts.luxonis.com/artifactory/luxonis-depthai-data-local/network/mobilenet-ssd_openvino_2021.2_8shave.blob"
    SHA1 "3329bb8f3a9c881ef9756d232055f9d6f38aa07b"
    FILE "mobilenet-ssd_openvino_2021.2_8shave.blob"
    LOCATION mobilenet_blob
)

# OpenVINO 2020.3 blob
hunter_private_data(
    URL "https://artifacts.luxonis.com/artifactory/luxonis-depthai-data-local/network/text-image-super-resolution-0001_2020.3_4shave.blob"
    SHA1 "f0134c9b843fe414f6d98b17a70f069d1ab0f3d8"
    FILE "text-image-super-resolution-0001_2020.3_4shave.blob"
    LOCATION openvino_2020_3_blob
)
# OpenVINO 2020.4 blob
hunter_private_data(
    URL "https://artifacts.luxonis.com/artifactory/luxonis-depthai-data-local/network/text-image-super-resolution-0001_2020.4_4shave.blob"
    SHA1 "25dcf0b146da8c85c9c4cba00ad5fdd4ed02a1b6"
    FILE "text-image-super-resolution-0001_2020.4_4shave.blob"
    LOCATION openvino_2020_4_blob
)

# OpenVINO 2021.1 blob
hunter_private_data(
    URL "https://artifacts.luxonis.com/artifactory/luxonis-depthai-data-local/network/text-image-super-resolution-0001_2021.1_4shave.blob"
    SHA1 "39c4f47f2a75627b7561e97dd7cdfcd0b1925a1e"
    FILE "text-image-super-resolution-0001_2021.1_4shave.blob"
    LOCATION openvino_2021_1_blob
)
# OpenVINO 2021.2 blob
hunter_private_data(
    URL "https://artifacts.luxonis.com/artifactory/luxonis-depthai-data-local/network/text-image-super-resolution-0001_2021.2_4shave.blob"
    SHA1 "a204467f86aa4ad63d31782ada271bea6f57f789"
    FILE "text-image-super-resolution-0001_2021.2_4shave.blob"
    LOCATION openvino_2021_2_blob
)
# OpenVINO 2021.3 blob
hunter_private_data(
    URL "https://artifacts.luxonis.com/artifactory/luxonis-depthai-data-local/network/text-image-super-resolution-0001_2021.3_4shave.blob"
    SHA1 "af19470feb59317e74d045bc31d93ca129c46674"
    FILE "text-image-super-resolution-0001_2021.3_4shave.blob"
    LOCATION openvino_2021_3_blob
)
# OpenVINO 2021.4.2 blob
hunter_private_data(
    URL "https://artifacts.luxonis.com/artifactory/luxonis-depthai-data-local/network/text-image-super-resolution-0001_2021.4.2_4shave.blob"
    SHA1 "164b6b2ae48d38bc4f07cc8296b8bcb7644a1578"
    FILE "text-image-super-resolution-0001_2021.4.2_4shave.blob"
    LOCATION openvino_2021_4_2_blob
)
# OpenVINO 2022.1.0 blob
hunter_private_data(
    URL "https://artifacts.luxonis.com/artifactory/luxonis-depthai-data-local/network/text-image-super-resolution-0001_2022.1.0_4shave.blob"
    SHA1 "98e94b865b9c48a92eaebd1ddc883712dfe7cfcb"
    FILE "text-image-super-resolution-0001_2022.1.0_4shave.blob"
    LOCATION openvino_2022_1_blob
)
# YoloV4 resource
hunter_private_data(
    URL "https://artifacts.luxonis.com/artifactory/luxonis-depthai-data-local/network/yolo-v4-tiny-tf_openvino_2021.4_4shave.blob"
    SHA1 "7da2f96f7300e3828940557e6a86ac6f243eef7e"
    FILE "yolo-v4-tiny-tf_openvino_2021.4_4shave.blob"
    LOCATION tiny_yolo_v4_2021-4_4shave_blob
)

# Superblob
hunter_private_data(
    URL "https://artifacts.luxonis.com/artifactory/luxonis-depthai-data-local/network/person-reidentification-retail-0277_openvino_2022.1_8shave.superblob"
    SHA1 "6f14e3a5388946d6de849ff4f6432702601b1003"
    FILE "person-reidentification-retail-0277_openvino_2022.1_8shave.superblob"
    LOCATION superblob_path
)

# NNarchives of different types
hunter_private_data(
    URL "https://artifacts.luxonis.com/artifactory/luxonis-depthai-data-local/network/nnarchive/yolo_blob_nnarchive.tar.xz"
    SHA1 "6b9697e5aaf1560efbdea7f8d7487bae51def619"
    FILE "yolo_blob_nnarchive.tar.xz"
    LOCATION yolo_blob_nnarchive_path
)

hunter_private_data(
    URL "https://artifacts.luxonis.com/artifactory/luxonis-depthai-data-local/network/nnarchive/yolo_superblob_nnarchive.tar.xz"
    SHA1 "2356fd7e5a203446211a891a74f0b23e0912eaf6"
    FILE "yolo_superblob_nnarchive.tar.xz"
    LOCATION yolo_superblob_nnarchive_path
)

hunter_private_data(
    URL "https://artifacts.luxonis.com/artifactory/luxonis-depthai-data-local/network/nnarchive/yolo_onnx_nnarchive.tar.xz"
    SHA1 "7abb6a8c05c5f66897cd2c5c6b4f0170620dff8b"
    FILE "yolo_onnx_nnarchive.tar.xz"
    LOCATION yolo_onnx_nnarchive_path
)

### On-host tests ####################################################################################
### On-host tests ####################################################################################
### On-host tests ####################################################################################

# Superblob test
dai_add_test(openvino_test src/onhost_tests/openvino/openvino_test.cpp onhost)
target_compile_definitions(openvino_test PRIVATE SUPERBLOB_PATH="${superblob_path}")

# NNArchive test
dai_add_test(nn_archive_test src/onhost_tests/nn_archive/nn_archive_test.cpp onhost)
target_compile_definitions(nn_archive_test PRIVATE
    BLOB_ARCHIVE_PATH="${yolo_blob_nnarchive_path}"
    SUPERBLOB_ARCHIVE_PATH="${yolo_superblob_nnarchive_path}"
    ONNX_ARCHIVE_PATH="${yolo_onnx_nnarchive_path}"
)

# Eeprom naming parsing tests
dai_add_test(naming_test src/onhost_tests/naming_test.cpp onhost)

# Image transformations test
dai_add_test(image_transformations_test src/onhost_tests/image_transformations_test.cpp onhost)

# MessageQueue tests
dai_add_test(message_queue_test src/onhost_tests/message_queue_test.cpp onhost)

# StreamMessageParser tests
dai_add_test(stream_message_parser_test src/onhost_tests/stream_message_parser_test.cpp onhost)

# Bootloader version tests
dai_add_test(bootloader_version_test src/onhost_tests/bootloader_version_test.cpp onhost)

# Bootloader configuration tests
dai_add_test(bootloader_config_test src/onhost_tests/bootloader_config_test.cpp onhost)

dai_add_test(env_test src/onhost_tests/env_test.cpp onhost)

### On-device tests ####################################################################################
### On-device tests ####################################################################################
### On-device tests ####################################################################################

# Depthai logging test
dai_add_test(logging_test src/ondevice_tests/logging_test.cpp ondevice)

# OpenVINO blob test
dai_add_test(openvino_blob_test src/ondevice_tests/openvino_blob_test.cpp ondevice)
dai_test_compile_definitions(openvino_blob_test PRIVATE
    OPENVINO_2020_3_BLOB_PATH="${openvino_2020_3_blob}"
    OPENVINO_2020_4_BLOB_PATH="${openvino_2020_4_blob}"
    OPENVINO_2021_1_BLOB_PATH="${openvino_2021_1_blob}"
    OPENVINO_2021_2_BLOB_PATH="${openvino_2021_2_blob}"
    OPENVINO_2021_3_BLOB_PATH="${openvino_2021_3_blob}"
    OPENVINO_2021_4_BLOB_PATH="${openvino_2021_4_2_blob}"
    OPENVINO_2022_1_BLOB_PATH="${openvino_2022_1_blob}"
)


# Neural network test
dai_add_test(neural_network_test src/ondevice_tests/neural_network_test.cpp ondevice)
dai_test_compile_definitions(neural_network_test PRIVATE BLOB_PATH="${mobilenet_blob}")

# Color camera test
dai_add_test(color_camera_node_test src/ondevice_tests/color_camera_node_test.cpp ondevice)

# Image manip test
dai_add_test(image_manip_node_test src/ondevice_tests/image_manip_node_test.cpp ondevice)

# Pipeline test
dai_add_test(pipeline_test src/ondevice_tests/pipeline_test.cpp ondevice)

# Device USB Speed and serialization macros test
dai_add_test(device_usbspeed_test    src/ondevice_tests/device_usbspeed_test.cpp ondevice CONFORMING)
dai_add_test(device_usbspeed_test_17 src/ondevice_tests/device_usbspeed_test.cpp ondevice CONFORMING CXX_STANDARD 17)
dai_add_test(device_usbspeed_test_20 src/ondevice_tests/device_usbspeed_test.cpp ondevice CONFORMING CXX_STANDARD 20)

# Filesystem test
dai_add_test(filesystem_test src/ondevice_tests/filesystem_test.cpp ondevice)
dai_test_compile_definitions(filesystem_test PRIVATE BLOB_PATH="${mobilenet_blob}")
dai_add_test(filesystem_test_17 src/ondevice_tests/filesystem_test.cpp ondevice CXX_STANDARD 17)
dai_test_compile_definitions(filesystem_test_17 PRIVATE BLOB_PATH="${mobilenet_blob}")
dai_add_test(filesystem_test_20 src/ondevice_tests/filesystem_test.cpp ondevice CXX_STANDARD 20)
dai_test_compile_definitions(filesystem_test_20 PRIVATE BLOB_PATH="${mobilenet_blob}")

# Encoded frame test
dai_add_test(encoded_frame_test src/ondevice_tests/encoded_frame_test.cpp ondevice CXX_STANDARD 17)

# MessageGroup tests
dai_add_test(message_group_frame_test src/ondevice_tests/message_group_test.cpp ondevice CXX_STANDARD 17) 

# Pointcloud test
dai_add_test(pointcloud_test src/ondevice_tests/pointcloud_test.cpp ondevice)

# Resolutions test
dai_add_test(resolutions_test src/ondevice_tests/resolutions_test.cpp ondevice)

# Serialization test
dai_add_test(serialization_test src/onhost_tests/serialization_test.cpp ondevice)

# Detection network test
dai_add_test(detection_network_test src/ondevice_tests/pipeline/node/detection_network_test.cpp ondevice)
target_compile_definitions(detection_network_test PRIVATE
    BLOB_ARCHIVE_PATH="${yolo_blob_nnarchive_path}"
    SUPERBLOB_ARCHIVE_PATH="${yolo_superblob_nnarchive_path}"
    ONNX_ARCHIVE_PATH="${yolo_onnx_nnarchive_path}"
)